group 'org.frice'
version '1.7.11'

buildscript {
	ext.kotlin_version = '1.2.10'
	ext.dokka_version = '0.9.15'

	repositories {
		jcenter()
		mavenCentral()
	}

	dependencies {
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
		classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
	}
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	flatDir { dirs 'lib' }
}

sourceSets {
	main.kotlin.srcDirs = ['src']
	main.java.srcDirs = ['src']
	main.resources.srcDirs = ['res']
	test.kotlin.srcDirs = ['test']
	test.java.srcDirs = ['test']
}

configurations { library }

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
	compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
	library name: 'jl1.0.1'
	library name: 'mp3spi1.9.5'
	library name: 'tritonus_share'

	configurations.compile.extendsFrom(configurations.library)

	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

//noinspection GroovyAssignabilityCheck
tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

jar {
	from { configurations.library.collect { it.isDirectory() ? it : zipTree(it) } }
}

task fatJar(type: Jar) {
	classifier = 'all'
	description 'Assembles a jar archive containing the main classes and all the dependencies.'
	group 'build'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	description 'Assembles a jar archive containing the source code of this project.'
	group 'build'
	from sourceSets.main.allSource
}

dokka {
	outputFormat = 'html'
	outputDirectory = javadoc.destinationDir

	includes = ['LICENSE', 'README.md']
	// samples = ['test/org/frice/Test.kt', 'test/org/frice/JfxTest.kt']
	impliedPlatforms = ['JVM']

	jdkVersion = 8

	skipDeprecated = false
	reportNotDocumented = false
	noStdlibLink = false

	linkMapping {
		dir = 'src'
		url = 'https://github.com/icela/FriceEngine/blob/master/src'
		suffix = '#L'
	}

	// externalDocumentationLink { url = new URL("https://icela.github.io/") }
}

task javadok(type: Jar, dependsOn: [dokka, javadoc]) {
	classifier = 'javadoc'
	description 'Assembles a jar archive containing the javadoc of this project.'
	group 'documentation'
	from javadoc.destinationDir
}

artifacts {
	archives jar
	archives fatJar
	archives sourcesJar
	archives javadok
}
